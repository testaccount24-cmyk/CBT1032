XPCRTM   CSECT
*/********************************************************************/
*/*                                                                  */
*/* Name:        XPCRTM                                              */
*/*                                                                  */
*/* Function:    TCB-mode Recovery Termination Manager               */
*/*                                                                  */
*/* Description: Performs recovery functions on behalf of all        */
*/*              unlocked ZXPC server routines.                      */
*/*                                                                  */
*/* Input Regs:  When the SDWA is supplied the contents of the       */
*/*              registers on entry are as follows:                  */
*/*                                                                  */
*/*              R0     Input parameter                              */
*/*              R1     SDWA address                                 */
*/*              R2-R12 Unpredictable                                */
*/*              R13    Savearea address                             */
*/*              R14    Return Address                               */
*/*              R15    Entry address                                */
*/*                                                                  */
*/*              When the SDWA is NOT supplied, the contents of      */
*/*              the registers on entry are as follows:              */
*/*                                                                  */
*/*              R0     12 (decimal)                                 */
*/*              R1     ABEND completion code                        */
*/*              R2     Address of parmlist or zero                  */
*/*              R3-R13 Unpredictable                                */
*/*              R14    Return address                               */
*/*              R15    Entry address                                */
*/*                                                                  */
*/* Regs Usage:  R8  ZRWA address                                    */
*/*              R9  SDWA address                                    */
*/*              R10 Base address                                    */
*/*              R11 ZXVT address                                    */
*/*              R12 DSA  address                                    */
*/*              R13 Savearea address                                */
*/*                                                                  */
*/* Output Regs: R0  Reason code                                     */
*/*              R1  Output parameter                                */
*/*              R15 Return code                                     */
*/*                                                                  */
*/* Attributes:  TCB-mode                                            */
*/*              ESTAE issuer protect key                            */
*/*              Supervisor state                                    */
*/*              Reentrant                                           */
*/*              Dynamic-LPA resident                                */
*/*                                                                  */
*/* Copyright:   Ben Marino. 2021 - 2022 (unpublished)               */
*/*                                                                  */
*/********************************************************************/
*---------------------------CHANGE LOG--------------------------------*
*                                                                     *
* ID    Rel    Date       Author        Comments                      *
*---------------------------------------------------------------------*
* @01 | V1R1 | 12072021 | Ben Marino  | Initial version               *
*     |      |          |             |                               *
*     |      |          |             |                               *
***********************************************************************
*--------+---------+---------+---------+---------+---------+---------+*
XPCRTM   AMODE 31
XPCRTM   RMODE ANY
         USING XPCRTM,R15              entry
         B     RTMINIT                 branch
         ZZMAC GEN=MID,DSECT=NO        module ID
*
RTMINIT  DS    0H
         CL    R0,NOSDWA               SDWA available?
         BNE   RTMSAVE                 yes
         DROP  R15
         LA    R15,0                   percolate
         BR    R14                     return to RTM
*
RTMSAVE  DS    0H
         STM   R14,R12,12(R13)         save RTM regs
         LR    R10,R15                 copy base
         USING XPCRTM,R10              set addressability
         LR    R9,R1                   SDWA address
         USING SDWA,R9                 set addressability
         L     R8,SDWAPARM             ZRWA address
         USING ZRWA,R8                 set addressability
         LA    R1,ZRWASAVE             savearea address
         ST    R13,GPR@BWD(,R1)        chain savearea
         LR    R13,R1                  set savearea.
*
**       Capture diagnostic data
*
         TM    SDWAERRD,SDWACLUP       clean up only?
         BNZ   RTMPERC                 yes, percolate
         TM    SDWAERRC,SDWAPERC       percolated ABEND
         BNZ   RTMPERC                 yes, percolate
         TM    ZRWAFLG1,ZRWA@PER       recursive ABEND?
         BNZ   RTMPERC                 yes, percolate
         OI    ZRWAFLG1,ZRWA@PER       set recursion indicator
         XC    ZRWAWORK,ZRWAWORK       clear work flags
         MVC   ZRWASUBN,MIDSUB         server name
         MVC   ZRWAAPSW,SDWAEC2        copy ABEND PSW
         MVC   ZRWABEND,SDWAABCC       copy ABEND code
         MVC   ZRWATEA,SDWATRN2        copy translation address
         MVC   ZRWAILC,SDWAILC2        copy instruction length code
         MVC   ZRWAIC,SDWAICD2         copy interrupt code
         MVC   ZRWARECR,XPCRTMID       copy recovery routine name
*
**       Issue ABEND WTO message
*
         MVC   ZRWAWTO(XPC999EL),XPC999E
         MVC   ZRWAWSUB,ZRWASUBN       show server
         MVC   ZRWAWMOD,ZRWALMOD       show module
         ZZMAC GEN=CNVX,ZRWACONV,ZRWABEND,TR=TRTABLE
         MVC   ZRWACODE,ZRWACONV       save abend code
         MVI   ZRWATYPE,C'U'
         MVC   ZRWAWCOD,ZRWAUSER
         CLI   ZRWAUSER,C'0'           user ABEND code?
         BNE   RTM0050                 yes
         MVI   ZRWATYPE,C'S'           no
         MVC   ZRWAWCOD,ZRWASYS        system ABEND code
*
RTM0050  DS    0H
         ZZMAC GEN=CNVX,ZRWACONV,ZRWARSN,TR=TRTABLE
         MVC   ZRWAWTYP,ZRWATYPE       show ABEND type
         MVC   ZRWAWRSN,ZRWACONV       show reason code
         TM    ZRWAFLG1,ZRWA@NWT       WTO=NO?
         BNZ   RTM0060                 yes, skip msg
         WTO   MF=(E,ZRWAWTO)          issue ABEND WTO
*
**       Take SVC dump
*
RTM0060  DS    0H
         TM    ZRWAFLG1,ZRWA@NDP       SDUMP=NO?
         BNZ   RTMRTRY                 yes, skip dump
         MVC   ZRWADUMP(SDUMPL),SDUMP
         MVC   ZRWADSUB,ZRWASUBN       show server
         MVC   ZRWADMOD,ZRWALMOD       show module name
         MVC   ZRWADCOD,ZRWATYPE       show ABEND code
         MVC   ZRWADRSN,ZRWACONV       show reason code
         LA    R1,ZRWADUSR             show,
         ST    R1,ZRWADATA             dump title
         IPK   ,                       save key
         SPKA  0(0)                    set supervisor key
         SDUMP MF=(E,ZRWADUMP)         dump
         SPKA  0(2)                    restore key
         ST    R15,ZRWASDRC            save SDUMP RC
*
**       Retry
*
RTMRTRY  DS    0H
         ICM   R3,15,ZRWARTRY          retry address init?
         BZ    RTMPERC                 no, percolate
         MVC   SDWASRSV,ZRWAGPRS       set retry regs
         MVC   SDWACSCT,ZRWASECT       PC routine name
         SETRP RC=4,                   retry                           +
               WKAREA=(9),             SDWA address                    +
               RETADDR=(3),            retry address                   +
               RETREGS=YES,            retry regs                      +
               FRESDWA=YES,            release SDWA                    +
               RECPARM=ZRWARECP        recovery data
         B     RTMEXIT                 exit
*
**       Percolate
*
RTMPERC  DS    0H
         TM    ZRWAFLG1,ZRWA@LOG       LOGREC requested?
         BZ    RTMPERC0                no
         SETRP RECORD=YES,WKAREA=(9)   yes
*
RTMPERC0 DS    0H
         SETRP RC=0,WKAREA=(9)         percolate ABEND
*
RTMEXIT  DS    0H
         L     R13,GPR@BWD(,R13)       RTM savearea
         LM    R14,R12,GPR@R14(R13)    restore RTM regs
         BR    R14                     return to RTM
*---------------------------------------------------------------------*
*        Recovery data                                                *
*--------+---------+---------+---------+---------+---------+---------+*
         DS    0F
NOSDWA   DC    AL4(12)                 SDWA not supplied
TRTABLE  DC    CL16'0123456789ABCDEF'  translate table
XPCRTMID DC    CL8'XPCRTM'             XPCRTM ID
*---------------------------------------------------------------------*
*        WTO Error Message                                            *
*--------+---------+---------+---------+---------+---------+---------+*
         DS    0F
XPC999E  WTO   'XPC999E ZXPC SERVICE ERROR MODnnnnn ABEND=0000 RSN=0000+
               0000',ROUTCDE=(1,11),DESC=(4),MF=L
XPC999EL EQU   *-XPC999E
XPC999MX DS    0XL(70-XPC999EL+1)      ZRWA map restriction
*---------------------------------------------------------------------*
*        SDUMP Title                                                  *
*--------+---------+---------+---------+---------+---------+---------+*
         DS    0F
SDUMP    SDUMP HDR='ZXPC SERVICE ERROR MODnnnnn 0000/00000000',        +
               SDATA=(SQA,CSA,NUC,LSQA,RGN,LPA,TRT),MF=L
SDUMPL   EQU   *-SDUMP
SDUMPMAX DS    0XL(84-SDUMPL+1)        ZRWA map restriction
*---------------------------------------------------------------------*
*        Literals                                                     *
*--------+---------+---------+---------+---------+---------+---------+*
         LTORG ,
         DROP  ,
*---------------------------------------------------------------------*
*        Server Data Areas                                            *
*--------+---------+---------+---------+---------+---------+---------+*
         PRINT NOGEN
         ZZXVT ,
         ZZXRE ,
         ZZRWA ,
         ZZEQU ,
*---------------------------------------------------------------------*
*        System Data Areas                                            *
*--------+---------+---------+---------+---------+---------+---------+*
         PRINT NOGEN
         IHAPSA ,
         IHAASCB ,
         IHAASSB ,
         IHAASXB ,
         IHASDWA ,
         CVT   DSECT=YES
         END
