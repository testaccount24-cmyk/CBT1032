XPCFRR   CSECT
*/********************************************************************/
*/*                                                                  */
*/* Name:        XPCFRR                                              */
*/*                                                                  */
*/* Function:    FRR Recovery Termination Manager                    */
*/*                                                                  */
*/* Description: Performs recovery on behalf of SRB-mode routines    */
*/*              and locked TCB-mode routines.                       */
*/*                                                                  */
*/* Input Regs:  R1  SDWA address                                    */
*/*              R14 Return address                                  */
*/*              R15 Entry address                                   */
*/*                                                                  */
*/* Regs Usage:  R7  FRR address                                     */
*/*              R8  SDWA address                                    */
*/*              R9  ZRWA address                                    */
*/*              R10 Base address                                    */
*/*                                                                  */
*/* Output Regs: R14 Return address                                  */
*/*                                                                  */
*/* Attributes:  TCB-mode                                            */
*/*              SRB-mode                                            */
*/*              Primary mode                                        */
*/*              Key 0                                               */
*/*              Supervisor state                                    */
*/*              Page-fixed                                          */
*/*              Write-protected                                     */
*/*              Dynamic-LPA resident                                */
*/*                                                                  */
*/* Copyright:   Ben Marino. 2021 - 2022 (unpublished)               */
*/*                                                                  */
*/********************************************************************/
*---------------------------CHANGE LOG--------------------------------*
*                                                                     *
* ID    Rel    Date       Author        Comments                      *
*---------------------------------------------------------------------*
* @01 | V1R1 | 12072021 | Ben Marino  | Initial version               *
*     |      |          |             |                               *
*     |      |          |             |                               *
***********************************************************************
*--------+---------+---------+---------+---------+---------+---------+*
XPCFRR   AMODE 31
XPCFRR   RMODE ANY
         USING XPCFRR,R15              entry
         B     FRRMAIN                 branch
         ZZMAC GEN=MID,DSECT=NO        module ID
         DROP  R15
*
FRRMAIN  DS    0H
         LR    R10,R15                 base
         USING XPCFRR,R10              set addressability
         LR    R8,R1                   SDWA address
         USING SDWA,R8                 set addressability
         L     R7,SDWAPARM             FRR parm area
         USING FRR,R7                  set addressability
         L     R9,FRRZRWA              SRWA address
         USING ZRWA,R9                 set addressability
*
**       Retry recursion check
*
         TM    SDWAERRD,SDWACLUP       clean up only?
         BNZ   ABEND                   yes, percolate
         TM    SDWAERRC,SDWAPERC       percolated ABEND?
         BNZ   ABEND                   yes, percolate
         TM    FRRFLG1,FRR@RECR        recursive ABEND?
         BNZ   ABEND                   yes, percolate
         OI    FRRFLG1,FRR@RECR        set recursion indicator
*
**       Program interrupt check
*
         TM    FRRFLG1,FRR@PICC        PIC requested?
         BZ    RETRY                   no
         TM    SDWAERRA,SDWAPCHK       program check?
         BZ    ABEND                   no, percolate
         MVI   FRRXRC,X'04'            set RC
         CLI   SDWAICD1,PIC4           protection exception?
         BE    RETRY                   yes, retry
         MVI   FRRXRC,X'08'            set RC
         CLI   SDWAICD1,PIC5           addressing exception?
         BE    RETRY                   yes, retry
         MVI   FRRXRC,X'12'            set RC
         CLI   SDWAICD1,PIC10          segment translation exception?
         BE    RETRY                   yes, retry
         MVI   FRRXRC,X'16'            set RC
         CLI   SDWAICD1,PIC11          page transaltion exception
         BNE   ABEND                   no, percolate
*
**       Retry Routine
*
RETRY    DS    0H
         ICM   R3,15,ZRWARTRY          retry address init?
         BZ    ABEND                   no, percolate
         MVC   SDWASRSV,ZRWAGPRS       set retry regs
         MVC   SDWACSCT,ZRWASECT       routine name
         SETRP RC=4,                                                   +
               RETADDR=(3),            retry address                   +
               RETREGS=YES,            restore registers               +
               FRELOCK=LOCAL,          release LOCAL lock              +
               WKAREA=(8)              SDWA address
         BR    R14                     return to RTM
*
**       Percolate routine
*
ABEND    DS    0H
         TM    FRRFLG1,FRR@DUMP        SDUMP requested?
         BZ    ABEND00                 no
         SETRP DUMP=YES,WKAREA=(8)     yes
*
ABEND00  DS    0H
         TM    FRRFLG1,FRR@LOGR        LOGREC requested?
         BZ    ABEND10                 no
         SETRP RECORD=YES,WKAREA=(8)   yes
*
ABEND10  DS    0H
         MVC   SDWARECP,ZFRRPARM       recovery parm
         MVC   SDWACSCT,ZRWASECT       routine name
         SETRP RC=0,WKAREA=(8)         percolate ABEND
         BR    R14                     return to RTM
*---------------------------------------------------------------------*
*        Constants                                                    *
*--------+---------+---------+---------+---------+---------+---------+*
ZFRRPARM DS    0CL24
         DC    CL8'XPCSRV'             PC router name
         DC    CL8'XPC???'             PC routine name
         DC    CL8'XPCFRR'             Recovery routine name
ZFRR@SZE EQU   *-ZFRRPARM              parms size
*---------------------------------------------------------------------*
*        Literals                                                     *
*--------+---------+---------+---------+---------+---------+---------+*
         LTORG ,
         DROP  ,
*---------------------------------------------------------------------*
*        Server Data Areas                                            *
*--------+---------+---------+---------+---------+---------+---------+*
         PRINT NOGEN
         ZZXVT ,
         ZZRWA ,
         ZZEQU ,
*---------------------------------------------------------------------*
*        System Data Areas                                            *
*--------+---------+---------+---------+---------+---------+---------+*
         PRINT NOGEN
         IHAPSA ,
         CVT   DSECT=YES
         IHAFRRS ,
         IHASDWA ,
         END
